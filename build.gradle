buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath group: 'gchq', name: 'urlDependencyPlugin', version: 'v0.1'
    }
}
apply plugin: 'gchq.urldependencies'


task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

ext.versions = [
        stroom: '6.0-beta.1-SNAPSHOT',
        stroomMessaging: '1.0-SNAPSHOT',
        dropwizard: '1.0.5',
        slf4j: '1.7.21',
        logback: '1.1.8',
        hbase: '1.2.0',
        curator: '2.11.1',
        //0.10.2.0 is available but seems to have issues with spring_kafka_test
        kafka: '0.10.1.1',
        //same version for both stroom-query-api and stroom-query-common
        stroomQuery: 'v1.0.0-SNAPSHOT.2',
        stroomExpression: 'v1.0.0-SNAPSHOT.1'
]

//lib names for urlDependenciesPlugin
ext.urlLibs = [
        stroomQueryApi: "stroom-query-api-$versions.stroomQuery",
        stroomQueryCommon: "stroom-query-common-$versions.stroomQuery",
        stroomExpression: "stroom-expression-$versions.stroomExpression"
]

//3rd Party libs
ext.libs = [
    slf4j_api: "org.slf4j:slf4j-api:$versions.slf4j",
    log4j_over_slf4j: "org.slf4j:log4j-over-slf4j:$versions.slf4j",
    jcl_over_slf4j: "org.slf4j:jcl-over-slf4j:$versions.slf4j",

    assertj: "org.assertj:assertj-core:3.6.2",
    curator_framework: 'org.apache.curator:curator-framework:2.11.1',
    curator_recipies: 'org.apache.curator:curator-recipes:2.11.1',
    curator_service_discovery: 'org.apache.curator:curator-x-discovery:2.11.1',
    byte_buddy: "net.bytebuddy:byte-buddy:1.5.8",
    commons_lang: "commons-lang:commons-lang:2.6",
    curator_recipies: "org.apache.curator:curator-recipes:$versions.curator",
    curator_test: "org.apache.curator:curator-test:$versions.curator",
    dropwizard_core: "io.dropwizard:dropwizard-core:$versions.dropwizard",
    dropwizard_hibernate: "io.dropwizard:dropwizard-hibernate:$versions.dropwizard",
    dropwizard_testing: "io.dropwizard:dropwizard-testing:$versions.dropwizard",
    dropwizard_client: "io.dropwizard:dropwizard-client:$versions.dropwizard",
    dropwizard_auth_jwt: "com.github.toastshaman:dropwizard-auth-jwt:1.0.2-0",
    dropwizard_jobs_core: "de.spinscale.dropwizard:dropwizard-jobs-core:2.0.1",
    ehcache: "org.ehcache:ehcache:3.2.0",
    guava: "com.google.guava:guava:14.0",
    guice: "com.google.inject:guice:4.0",
    hbase_shaded_client: "org.apache.hbase:hbase-shaded-client:$versions.hbase",
    javaslang: 'io.javaslang:javaslang:2.0.5',
    joda_time: 'joda-time:joda-time:2.9.4',
    junit: "junit:junit:4.12", 
    kafka_clients: "org.apache.kafka:kafka-clients:$versions.kafka",
    kafka_streams: "org.apache.kafka:kafka-streams:$versions.kafka",
    kryo: "com.esotericsoftware:kryo-shaded:4.0.0",
    logback_classic: "ch.qos.logback:logback-classic:$versions.logback",
    logback_core: "ch.qos.logback:logback-core:$versions.logback",
    mockito_core: "org.mockito:mockito-core:2.+",
    mysql_connector: "mysql:mysql-connector-java:5.1.40",
    saxon: "net.sf.saxon:Saxon-HE:9.6.0-6",
    //instead of spring_kafka_test could use 'org.apache.kafka:kafka-streams-examples'
    spring_kafka_test: "org.springframework.kafka:spring-kafka-test:1.1.3.RELEASE"
] 

apply plugin: 'idea'

//apply from: "properties.gradle"

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    group 'stroom'
    version '1.0-SNAPSHOT'
    sourceCompatibility = 1.8

    repositories {
        jcenter()
        mavenLocal()
        maven {
            url "http://repository.cloudera.com/artifactory/cloudera-repos/"
        }
    }

    // This task lets us publish source. Useful for debugging.
    task sourceJar(type: Jar){
        from sourceSets.main.allJava
        classifier 'sources'
    }

}

//Configure common dependencies for all subProjects except the hbase filter
configure (subprojects - project(':stroom-stats-hbase-filter')) {

    dependencies {
        //Use Logback for logging
        compile libs.slf4j_api
        compile libs.log4j_over_slf4j
        compile libs.jcl_over_slf4j
        compile libs.logback_classic
        compile libs.logback_core

        testCompile libs.mockito_core
        testCompile libs.junit
        testCompile libs.assertj
    }
}

urlDependencies {
    libs "libs"
    compile(urlLibs.stroomQueryApi,
            "https://github.com/gchq/stroom-query/releases/download/$versions.stroomQuery/${urlLibs.stroomQueryApi}.jar")
    compile(urlLibs.stroomQueryCommon,
            "https://github.com/gchq/stroom-query/releases/download/$versions.stroomQuery/${urlLibs.stroomQueryCommon}.jar")
    compile(urlLibs.stroomExpression,
            "https://github.com/gchq/stroom-expression/releases/download/$versions.stroomExpression/${urlLibs.stroomExpression}.jar")
}

project(':stroom-stats-service') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId 'stats-service'
                from components.java
                artifact sourceJar
            }
        }
    }
}

project(':stroom-stats-api') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId 'stats-api'
                from components.java
                artifact sourceJar
            }
        }
    }
}
