plugins {
    //errorprone plugin relies on guava 21 and will not work on guava 20
    id "net.ltgt.errorprone" version "0.0.10" 
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'application'
mainClassName = 'stroom.stats.service.startup.App'

jar {
    manifest {
        attributes(
                "Implementation-Title": "stroom-stats",
                "Implementation-Version": version,
                "Main-Class" : "stroom.stats.service.startup.App"
        )
    }
    //archiveName 'stroom-stats.jar'
}

shadowJar {
    // A transformer to merges files in META-INF/services
    mergeServiceFiles()
    //archiveName 'stroom-stats-all.jar'
}

dependencies {
    //Project dependencies
    compile project(':stroom-stats-api')
    compile project(':stroom-stats-hbase-client')
    compile project(':stroom-stats-util')
    compile project(':stroom-stats-model')


    // TODO: add HBase-client dependency here, be sure to include the 'all' classifier
    //compile 'stroom:stats:1.0-SNAPSHOT:all'

    //Non-project stroom dependencies
    //compile 'stroom:stroom-query-api:5.0-beta.9-SNAPSHOT'
    compile urlDependencies.get(urlLibs.stroomQueryApi)
    compile urlDependencies.get(urlLibs.stroomQueryCommon)
    compile urlDependencies.get(urlLibs.stroomExpression)

    //Third party dependencies
    compile libs.curator_client
    compile libs.curator_framework
    compile libs.curator_recipies
    compile libs.curator_service_discovery
    compile libs.dropwizard_auth
    compile libs.dropwizard_auth_jwt
    compile libs.dropwizard_core
    compile libs.dropwizard_jetty
    compile libs.dropwizard_jersey
    compile libs.dropwizard_db
    compile libs.dropwizard_hibernate
    compile libs.dropwizard_jersey
    compile libs.dropwizard_jobs_core
    compile libs.dropwizard_lifecycle
    compile libs.dropwizard_metrics_annotation
    compile libs.dropwizard_metrics_healthchecks
    compile libs.dropwizard_servlets
    compile libs.dropwizard_configuration
    compile libs.ehcache
    compile libs.find_bugs
    compile libs.guava
    compile libs.guice
    compile libs.hbase_shaded_client
    compile libs.hibernate_core
    compile libs.hibernate_jpa_api
    compile libs.jackson_annotations
    compile libs.javaslang
    compile libs.javassist
    compile libs.javax_inject
    compile libs.javax_validation_api
    compile libs.javax_ws_rs_api
    compile libs.jersey_server
    compile libs.jersey_client
    compile libs.jose4j
    compile libs.kafka_clients
    compile libs.kafka_streams
    compile libs.kryo
    compile libs.logback_classic
    compile libs.objenesis //used for Kryo strategies
    compile libs.slf4j_api
    compile libs.xml_apis
    compile libs.zookeeper

    runtime libs.mysql_connector
    runtime libs.saxon

    testCompile project(':stroom-stats-mocks')

    testCompile libs.hamcrest_core
    testCompile libs.assertj
    testCompile libs.junit

    //deps for integrationTest
    testCompile libs.dropwizard_client
    testCompile libs.fastClasspathScanner
    testCompile libs.dropwizard_testing
    testCompile libs.spring_kafka_test
    
    //TODO gradle-dependency-analyze doesn't understand about integrationTest so says these libs are unused
    permitTestUnusedDeclared libs.dropwizard_testing
    permitTestUnusedDeclared libs.spring_kafka_test
    permitTestUnusedDeclared libs.dropwizard_client
    permitTestUnusedDeclared libs.fastClasspathScanner
    permitTestUnusedDeclared libs.objenesis //TODO Not sure why gradle-dependency-analyze is moaning about this

}

run {
    args 'server', './config.yml'
}

sourceSets {
    // This lets us use different directories for our integration tests.
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    //prevent multiple logger bindings as dropwizard uses logback
    all*.exclude group:'org.slf4j', module:'slf4j-log4j12'
    all*.exclude group:'log4j', module:'log4j'
}

configurations {
    // This means our integration tests get all the dependencies from our tests and we don't need to specify them twice.
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

// This task lets us run the actual integration tests.
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

// These lines pull the integration test task into our build process.
check.dependsOn integrationTest
integrationTest.mustRunAfter test

// This means the reports from our integration tests won't over-write the reports from our unit tests.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

